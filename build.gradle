// Gradle plugins
buildscript {
    repositories {
        maven {
            name = 'gradle-plugins'
            url = 'https://plugins.gradle.org/m2'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        // VanillaGradle contains a custom ForgeGradle extension for SpongeCommon (see below)
        classpath 'gradle.plugin.net.minecrell:vanillagradle:2.2-3'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'gradle.plugin.net.minecrell:licenser:0.3'
        classpath 'gradle.plugin.org.spongepowered:spongegradle:0.7'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
    }
}

// Create task to resolve SpongeAPI revision
apply from: 'api-revision.gradle'

configurations {
    fastutil
    compile {
        extendsFrom fastutil
    }
}

plugins.withType(MavenPlugin) {
    // Include fastutil in POM
    conf2ScopeMappings.addMapping(MavenPlugin.COMPILE_PRIORITY, configurations.fastutil, Conf2ScopeMappingContainer.COMPILE)
}

// Apply shared ForgeGradle configuration
ext.common = project
apply from: 'gradle/minecraft.gradle'

// Inherit SpongeCommon version from SpongeAPI
version = api.version
ext.apiVersion = version

dependencies {
    compile api
    compile('org.spongepowered:mixin:0.5.14-SNAPSHOT') {
        exclude module: 'launchwrapper'
    }

    fastutil 'it.unimi.dsi:fastutil:7.0.12'

    // log4j2 slf4j implementation
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'

    // Database connectors
    compile 'com.zaxxer:HikariCP:2.5.1'
    runtime 'org.mariadb.jdbc:mariadb-java-client:1.5.2'
    compile 'com.h2database:h2:1.4.192'
    runtime 'org.xerial:sqlite-jdbc:3.14.2.1'

    forgeGradleMcDeps('net.minecraft:launchwrapper:1.11') {
        transitive = false
    }

    testCompile 'org.spongepowered:launchwrappertestsuite:1.0-SNAPSHOT'
}

// Include API dependencies in our POM
ext.shadedDevProject = api

test {
    systemProperty 'org.spongepowered.test.launchwrapper.tweaker', 'org.spongepowered.common.launch.TestTweaker'
    workingDir = {test.temporaryDir}
}
